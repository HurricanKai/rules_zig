load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load(
    "@rules_zig//zig:defs.bzl",
    "zig_binary",
    "zig_configure_binary",
    "zig_configure_test",
    "zig_test",
)

platform(
    name = "aarch64-linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:aarch64",
    ],
)

zig_binary(
    name = "read_elf_arch",
    main = "read_elf_arch.zig",
)

# TODO[AH] Test target transition on zig_library.
#   Open question: how to extract the target platform from .a file?

# ----------------------------------------------------------
# zig_configure_binary

zig_binary(
    name = "binary",
    main = "main.zig",
    tags = ["manual"],
)

zig_configure_binary(
    name = "binary_aarch64-linux",
    actual = ":binary",
    tags = ["manual"],
    target = ":aarch64-linux",
)

build_test(
    name = "binary_aarch64-linux_build_test",
    targets = [
        ":binary_aarch64-linux",
    ],
)

genrule(
    name = "binary_aarch64-linux_arch",
    srcs = [":binary_aarch64-linux"],
    outs = ["binary_aarch64-linux_arch.actual"],
    cmd = "$(execpath :read_elf_arch) $(SRCS) > $(OUTS)",
    tools = [":read_elf_arch"],
)

diff_test(
    name = "binary_aarch64-linux_arch_test",
    file1 = ":binary_aarch64-linux_arch.expected",
    file2 = ":binary_aarch64-linux_arch.actual",
)

# TODO[AH] Test another operating system

# ----------------------------------------------------------
# zig_configure_test

zig_test(
    name = "test",
    main = "main.zig",
    tags = ["manual"],
)

zig_configure_test(
    name = "test_aarch64-linux",
    actual = ":test",
    tags = ["manual"],
    target = ":aarch64-linux",
)

build_test(
    name = "test_aarch64-linux_build_test",
    targets = [
        ":test_aarch64-linux",
    ],
)

genrule(
    name = "test_aarch64-linux_arch",
    testonly = True,
    srcs = [":test_aarch64-linux"],
    outs = ["test_aarch64-linux_arch.actual"],
    cmd = "$(execpath :read_elf_arch) $(SRCS) > $(OUTS)",
    tools = [":read_elf_arch"],
)

diff_test(
    name = "test_aarch64-linux_arch_test",
    file1 = ":test_aarch64-linux_arch.expected",
    file2 = ":test_aarch64-linux_arch.actual",
)

# TODO[AH] Test another operating system
